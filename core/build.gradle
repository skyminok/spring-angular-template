plugins {
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
}

subprojects { subproject ->
    if (!(subproject.name in ['core', 'features', 'commons'])) {
        apply plugin: 'java'
        apply plugin: 'java-library'
        apply plugin: 'pmd'

        repositories {
            mavenCentral()
        }

        configurations {
            app
            implementation.extendsFrom(app)
            runtimeOnly.extendsFrom(app)
            compileClasspath.extendsFrom(app)
            annotationProcessor.extendsFrom(app)
            testAnnotationProcessor.extendsFrom(app)
            api.extendsFrom(app)
        }

        configurations.all {
            resolutionStrategy {
                failOnVersionConflict()
                preferProjectModules()
            }
        }

        dependencies {
            app platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

            // utils
            annotationProcessor 'org.projectlombok:lombok'
            compileOnly 'org.projectlombok:lombok'
        }

        tasks.withType(JavaCompile) {
            options.incremental = true // one flag, and things will get MUCH faster
            options.encoding = 'UTF-8'
        }

        test {
            useJUnitPlatform()
            testLogging {
                events "started", "passed", "skipped", "failed"
                exceptionFormat 'full'
            }
            maxHeapSize = '1024m'
            minHeapSize = '1024m'
        }
    }
}