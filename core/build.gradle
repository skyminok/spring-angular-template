plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

subprojects { subproject ->
    if (!(subproject.name in ['core', 'features', 'commons'])) {
        apply plugin: 'java'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'java-library'
        apply plugin: 'pmd'

        sourceCompatibility = 14
        targetCompatibility = 14

        ext {
            springBootVersion = '2.3.4.RELEASE'
        }

        repositories {
            mavenCentral()
        }

        configurations.all {
            resolutionStrategy {
                failOnVersionConflict()
            }
        }

        dependencyManagement {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
            }
        }

        dependencies {
            // Spring Framework
            implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
            implementation 'org.springframework.boot:spring-boot-starter-web'

            // utils
            annotationProcessor 'org.projectlombok:lombok'
            compileOnly 'org.projectlombok:lombok'

            // test
            testImplementation('org.springframework.boot:spring-boot-starter-test') {
                exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
            }
            testAnnotationProcessor 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
        }

        tasks.withType(JavaCompile) {
            options.incremental = true // one flag, and things will get MUCH faster
            options.encoding = 'UTF-8'
        }

        test {
            useJUnitPlatform()
            testLogging {
                events "started", "passed", "skipped", "failed"
                exceptionFormat 'full'
            }
            maxHeapSize = '1024m'
            minHeapSize = '1024m'
        }
    }
}